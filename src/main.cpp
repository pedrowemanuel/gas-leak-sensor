/***
  Rui Santos & Sara Santos - Random Nerd Tutorials
  Complete instructions at https://RandomNerdTutorials.com/esp32-wi-fi-manager-asyncwebserver/
  Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files.
  The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
***/
#include <Arduino.h>
#include <WiFi.h>
#include <ESPAsyncWebServer.h>
#include <AsyncTCP.h>
#include "SPIFFS.h"
#include <WebSocketsServer.h>
#include <UniversalTelegramBot.h>
#include <WiFiClientSecure.h>

#define SSID_ESP "GAS-LEAK-SENSOR:200"

#define LED_PIN 2
#define BUZZER_PIN 13
#define A0_PIN GPIO_NUM_32
#define D0_PIN GPIO_NUM_25
#define SENSOR_THRESHOLD 400

WiFiClientSecure client;

// Checks for new messages every 1 second.
int botRequestDelay = 1000;
unsigned long lastTimeBotRan;

// Create AsyncWebServer object on port 80
AsyncWebServer server(80);

WebSocketsServer webSocket(81);

// Search for parameter in HTTP POST request
const char *PARAM_INPUT_1 = "ssid";
const char *PARAM_INPUT_2 = "pass";
const char *PARAM_INPUT_3 = "ip";
const char *PARAM_INPUT_4 = "telegramToken";
const char *PARAM_INPUT_5 = "websocketIP";

// Variables to save values from HTML form
String ssid;
String pass;
String ip;
String telegramToken;
String websocketIP;

// Use @myidbot to find out the chat ID of an individual or a group
// Also note that you need to click "start" on a bot before it can
// message you
String chatID;

// File paths to save input values permanently
const char *ssidPath = "/ssid.txt";
const char *passPath = "/pass.txt";
const char *ipPath = "/ip.txt";
const char *telegramTokenPath = "/telegramToken.txt";
const char *websocketIPPath = "/websocketip.txt";
const char *chatIDPath = "/chatid.txt";

IPAddress localIP;
// IPAddress localIP(192, 168, 1, 201); // hardcoded

// Set your Gateway IP address
IPAddress localGateway;
// IPAddress localGateway(192, 168, 1, 1); //hardcoded
IPAddress subnet(255, 255, 0, 0);

// Timer variables
unsigned long previousMillis = 0;
const long interval = 10000; // interval to wait for Wi-Fi connection (milliseconds)

String alarmState;

// Initialize SPIFFS
void initSPIFFS()
{
  if (!SPIFFS.begin(true))
  {
    Serial.println("An error has occurred while mounting SPIFFS");
    return;
  }
  Serial.println("SPIFFS mounted successfully");
}

// Read File from SPIFFS
String readFile(fs::FS &fs, const char *path)
{
  Serial.printf("Reading file: %s\r\n", path);

  File file = fs.open(path);
  if (!file || file.isDirectory())
  {
    Serial.println("- failed to open file for reading");
    return String();
  }

  String fileContent;
  while (file.available())
  {
    fileContent = file.readStringUntil('\n');
    break;
  }
  return fileContent;
}

// Write file to SPIFFS
void writeFile(fs::FS &fs, const char *path, const char *message)
{
  Serial.printf("Writing file: %s\r\n", path);

  File file = fs.open(path, FILE_WRITE);
  if (!file)
  {
    Serial.println("- failed to open file for writing");
    return;
  }
  if (file.print(message))
  {
    Serial.println("- file written");
  }
  else
  {
    Serial.println("- write failed");
  }
}

// Initialize WiFi
bool initWiFi()
{
  if (ssid == "" || ip == "" || websocketIP == "")
  {
    Serial.println("Undefined SSID or IP address.");
    return false;
  }

  WiFi.mode(WIFI_STA);
  localIP.fromString(ip.c_str());
  localGateway.fromString(telegramToken.c_str());

  // if (!WiFi.config(localIP, localGateway, subnet))
  // {
  //   Serial.println("STA Failed to configure");
  //   return false;
  // }
  WiFi.begin(ssid.c_str(), pass.c_str());
  Serial.println("Connecting to WiFi...");

  unsigned long currentMillis = millis();
  previousMillis = currentMillis;

  while (WiFi.status() != WL_CONNECTED)
  {
    currentMillis = millis();
    if (currentMillis - previousMillis >= interval)
    {
      Serial.println("Failed to connect.");
      return false;
    }
  }

  Serial.println("Connected to WiFi!");

  // Printar IP e MAC
  Serial.print("IP Address: ");
  Serial.println(WiFi.localIP());

  Serial.print("MAC Address: ");
  Serial.println(WiFi.macAddress());

  return true;
}

// Replaces placeholder with LED state value
String processor(const String &var)
{
  if (var == "STATE")
  {
    if (digitalRead(LED_PIN))
    {
      alarmState = "ON";
    }
    else
    {
      alarmState = "OFF";
    }
    return alarmState;
  }
  return String();
}

void alarmON()
{
  digitalWrite(LED_PIN, HIGH);
  digitalWrite(BUZZER_PIN, HIGH);

  delay(5000);
}

void alarmOFF()
{
  digitalWrite(LED_PIN, LOW);
  digitalWrite(BUZZER_PIN, LOW);
}

void reset()
{
  writeFile(SPIFFS, ssidPath, "");
  writeFile(SPIFFS, passPath, "");
  writeFile(SPIFFS, ipPath, "");
  writeFile(SPIFFS, websocketIPPath, "");
  writeFile(SPIFFS, chatIDPath, "");

  esp_restart();
}

TaskHandle_t ledTaskHandle; // Vari√°vel para armazenar a task

// Fun√ß√£o da task para acender o LED por 5 segundos
void ledTask(void *parameter)
{

  digitalWrite(LED_PIN, HIGH);          // Acende o LED
  vTaskDelay(500 / portTICK_PERIOD_MS); // Espera
  digitalWrite(LED_PIN, LOW);           // Apaga o LED
  vTaskDelay(500 / portTICK_PERIOD_MS); // Espera
  digitalWrite(LED_PIN, HIGH);          // Apaga o LED

  ledTaskHandle = NULL; // Reseta o handle da task
  vTaskDelete(NULL);    // Deleta a pr√≥pria task quando terminar
}

// Handle what happens when you receive new messages
void handleNewMessagesTelegram(UniversalTelegramBot bot, String telegramToken, int numNewMessages)
{
  Serial.println("handleNewMessages");
  Serial.println(String(numNewMessages));

  for (int i = 0; i < numNewMessages; i++)
  {
    // Chat id of the requester
    String chat_id = String(bot.messages[i].chat_id);

    // Print the received message
    String text = bot.messages[i].text;
    Serial.println(text);

    String from_name = bot.messages[i].from_name;

    if (text == "/start")
    {
      writeFile(SPIFFS, chatIDPath, chat_id.c_str());

      String welcome = "Bem vindo, " + from_name + ".\n";
      welcome += "A partir de agora voc√™ receber√° atualiza√ß√µes quando houver mudan√ßa do sensor.\n\n";
      bot.sendMessage(chat_id, welcome, "");
    }
  }
}

void loopTelegram(UniversalTelegramBot bot, String telegramToken)
{
  if (millis() > lastTimeBotRan + botRequestDelay)
  {
    int numNewMessages = bot.getUpdates(bot.last_message_received + 1);

    while (numNewMessages)
    {
      Serial.println("got response");
      handleNewMessagesTelegram(bot, telegramToken, numNewMessages);
      numNewMessages = bot.getUpdates(bot.last_message_received + 1);
    }
    lastTimeBotRan = millis();
  }
}

void initWebServerHTTP()
{
  // Connect to Wi-Fi network with SSID and password
  Serial.println("Setting AP (Access Point)");
  // NULL sets an open Access Point
  WiFi.softAP(SSID_ESP, NULL);

  IPAddress IP = WiFi.softAPIP();
  Serial.print("AP IP address: ");
  Serial.println(IP);

  // Web Server Root URL
  server.on("/", HTTP_GET, [](AsyncWebServerRequest *request)
            { request->send(SPIFFS, "/wifimanager.html", "text/html");
      Serial.println("wifimanager.html foi acessada!"); });

  server.serveStatic("/", SPIFFS, "/");

  server.on("/", HTTP_POST, [](AsyncWebServerRequest *request)
            {
                int params = request->params();
                for (int i = 0; i < params; i++)
                {
                  const AsyncWebParameter *p = request->getParam(i);
                  if (p->isPost())
                  {
                    // HTTP POST ssid value
                    if (p->name() == PARAM_INPUT_1)
                    {
                      ssid = p->value().c_str();
                      Serial.print("SSID set to: ");
                      Serial.println(ssid);
                      // Write file to save value
                      writeFile(SPIFFS, ssidPath, ssid.c_str());
                    }
                    // HTTP POST pass value
                    if (p->name() == PARAM_INPUT_2)
                    {
                      pass = p->value().c_str();
                      Serial.print("Password set to: ");
                      Serial.println(pass);
                      // Write file to save value
                      writeFile(SPIFFS, passPath, pass.c_str());
                    }
                    // HTTP POST ip value
                    if (p->name() == PARAM_INPUT_3)
                    {
                      ip = p->value().c_str();
                      Serial.print("IP Address set to: ");
                      Serial.println(ip);
                      // Write file to save value
                      writeFile(SPIFFS, ipPath, ip.c_str());
                    }
                    // HTTP POST telegramToken value
                    if (p->name() == PARAM_INPUT_4)
                    {
                      telegramToken = p->value().c_str();
                      Serial.print("Gateway set to: ");
                      Serial.println(telegramToken);
                      // Write file to save value
                      writeFile(SPIFFS, telegramTokenPath, telegramToken.c_str());
                    }

                    // HTTP POST websocket ip value
                    if (p->name() == PARAM_INPUT_5)
                    {
                      websocketIP = p->value().c_str();
                      Serial.print("websocket ip set to: ");
                      Serial.println(websocketIP);
                      // Write file to save value
                      writeFile(SPIFFS, websocketIPPath, websocketIP.c_str());
                    }
                    // Serial.printf("POST[%s]: %s\n", p->name().c_str(), p->value().c_str());
                  }
                }
                request->send(200, "text/plain", WiFi.macAddress()); });

  server.on("/restart", HTTP_GET, [](AsyncWebServerRequest *request)
            { esp_restart(); });
  server.begin();
};

void webSocketEvent(uint8_t num, WStype_t type, uint8_t *payload, size_t length)
{
  String event;

  switch (type)
  {
  case WStype_CONNECTED:
    Serial.printf("üì° Cliente conectado! ID: %u\n", num);
    webSocket.sendTXT(num, "Conex√£o estabelecida com ESP32!");
    break;
  case WStype_TEXT:
    Serial.printf("üì© Recebido do Cliente: %s\n", payload);

    event = String((char *)payload);
    webSocket.sendTXT(num, "üîÑ Mensagem recebida: " + event);

    if (event == "on")
    {
      alarmON();
    }

    if (event == "off")
    {
      alarmOFF();
    }

    if (event == "reset")
    {
      reset();
    }

    break;
  case WStype_DISCONNECTED:
    Serial.printf("‚ùå Cliente desconectado! ID: %u\n", num);
    break;
  }
}

void initWebSocket(String ip)
{
  IPAddress localWebsocketIP;

  if (ip == "")
  {
    Serial.println("Undefined WebsocketIP address.");
    return;
  }

  localWebsocketIP.fromString(ip.c_str());

  IPAddress subnet(255, 255, 255, 0);

  WiFi.softAPConfig(localWebsocketIP, localWebsocketIP, subnet);
  WiFi.softAP(SSID_ESP, "");

  Serial.println("üöÄ Wi-Fi AP Criado!");
  Serial.print("WebSocket dispon√≠vel em: ws://");
  Serial.print(WiFi.softAPIP());
  Serial.println(":81");

  webSocket.begin();
  webSocket.onEvent(webSocketEvent);
}

void setup()
{
  // Serial port for debugging purposes
  Serial.begin(115200);

  initSPIFFS();

  // Set GPIO 2 as an OUTPUT
  pinMode(LED_PIN, OUTPUT);
  pinMode(BUZZER_PIN, OUTPUT); // Define o PINO do buzzer como sa√≠da
  pinMode(A0_PIN, INPUT);
  pinMode(D0_PIN, INPUT);

  digitalWrite(LED_PIN, LOW);
  digitalWrite(BUZZER_PIN, LOW);

  // Load values saved in SPIFFS
  ssid = readFile(SPIFFS, ssidPath);
  pass = readFile(SPIFFS, passPath);
  ip = readFile(SPIFFS, ipPath);
  telegramToken = readFile(SPIFFS, telegramTokenPath);
  websocketIP = readFile(SPIFFS, websocketIPPath);

  Serial.println(ssid);
  Serial.println(pass);
  Serial.println(ip);
  Serial.println(telegramToken);
  Serial.println(websocketIP);

  if (initWiFi())
  {
    initWebSocket(websocketIP);
    client.setCACert(TELEGRAM_CERTIFICATE_ROOT);
  }
  else
  {
    initWebServerHTTP();
  }
}

int digitalSensorBefore = 1;

void loop()
{
  int analogSensor = analogRead(A0_PIN);
  String analogStringSensor = String(analogSensor);

  int digitalSensor = digitalRead(D0_PIN);
  String digitalStringSensor = String(digitalSensor);

  // Enviando mensagens via websocket
  webSocket.loop();
  if (webSocket.connectedClients() > 0)
  {
    webSocket.broadcastTXT("Sinal anal√≥gico: " + analogStringSensor + "ppm");

    if (digitalSensorBefore != digitalSensor)
    {
      webSocket.broadcastTXT(digitalStringSensor);
    }
  }
  // Enviando mensagens via telegram
  if (telegramToken != "")
  {
    UniversalTelegramBot bot(telegramToken, client);

    if (chatID != "")
    {
      if (digitalSensorBefore != digitalSensor && !digitalSensor)
      {
        bot.sendMessage(chatID, "Aviso! O sensor de vazamento de g√°s disparou o alarme!", "");
      }
    }

    loopTelegram(bot, telegramToken);
  }

  digitalSensorBefore = digitalSensor;

  Serial.print("Pin A0: ");
  Serial.println(analogSensor);

  Serial.print("Pin D0: ");
  Serial.println(digitalSensor);

  // GENRENCIA O ALARME CRITICO (BUZZER + LED CONSTANTE)
  if (!digitalSensor)
  {
    alarmON();
  }
  else
  {
    alarmOFF();

    // GENRENCIA O ALARME PREVENTIVO ( LED PISCANDO)
    if (analogSensor > SENSOR_THRESHOLD)
    {
      if (ledTaskHandle == NULL)
      {
        xTaskCreate(ledTask, "LedTask", 1000, NULL, 1, &ledTaskHandle);
      }
    }
  }

  delay(200);
}